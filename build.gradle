plugins {
    id "java"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group = "com.vmware"
    version = "0.0.1-SNAPSHOT"

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }
    
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.4"
        }
        dependencies {
            dependency "org.springframework.cloud:spring-cloud-stream-binder-kafka:3.1.4"
            dependency "org.springframework.cloud:spring-cloud-sleuth-zipkin:3.0.4"
            dependency "org.apache.commons:commons-lang3:3.12.0"
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:1.18.22"
        annotationProcessor "org.projectlombok:lombok:1.18.22"

        testCompileOnly "org.projectlombok:lombok:1.18.22"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.22"

        testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
    }

    test {
        useJUnitPlatform {
            excludeTags 'post'
        }
    }

    task postData(type: Test) {
        useJUnitPlatform {
            includeTags 'post'
        }
    }
}

